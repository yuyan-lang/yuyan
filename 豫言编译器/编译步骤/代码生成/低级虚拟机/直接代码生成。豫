
寻观标准库之书。
寻观豫言编译器之编译数据之抽象绑定树结构之抽象语法树之总集之书。

。
寻观豫言编译器之编译数据之编译配置之编译期输出之书。
寻观豫言编译器之编译步骤之总体过程之编译过程工具之书。






寻观拓展库之顺序标识符之顺序标识符之书。


寻观代码生成工具之书。

寻观豫言编译器之编译数据之编译配置之全局配置之书。

「： 包裹函数体乃化爻而化字符串而化字符串列列而字符串列列也。
包裹函数体者会公开而会函数名而会串而
    （【『define 』附（若公开则『』否则『internal 』）附默认调用规范附『 i64* @"』附函数名附『"』附（获取普通参数签名于【『stack_top』，『entry_block_id』，『argument_data』，『return_record』】）附『 {「：换行：」』】）衔
    『br label %"入口续延名_进入标签"』衔『「：换行：」』
        衔（（串）接【【『「：换行：」}「：换行：」』】】）也。 ：」

包裹函数体前乃化爻而化字符串而字符串列也。
包裹函数体前者会公开而会函数名而
    『define 』衔（若公开则『』否则『internal 』）衔默认调用规范衔『 i64* @"』衔函数名衔『"』衔
    （获取普通参数签名于【『stack_top』，『entry_block_id』，『argument_data』，『return_record』】）衔『 {「：换行：」』衔
    『%"argument_data_alloca" = alloca i64「：换行：」』衔
    『br label %"入口续延名_进入标签"「：换行：」』衔
    【】
        也。

包裹函数体后乃字符串列也。
包裹函数体后者【『「：换行：」}「：换行：」』】也。

直接代码生成乃化字符串「：防止重复命名的续延名：」而化（化字符串列而有）而
        化字符串而
        化表达式「：待变换的式子：」
         而字符串列「：字符串将赋值给返回名，包含换行：」也。
直接代码生成者循「直接代码生成」以会续延名而会写出而会返回名而会式而
    虑「
        保留字段者下个字符序数于『_』附续延名附『_重名_』也。

        「读名」者「读取直接变量」也。

        详细调试打印行于（会无而『直接代码生成中：』附（表达式表示于式））。

        默认返回乃化字符串列而字符串列也。
        默认返回者会串而
            『%"』衔返回名衔『" = 』衔（串接【『「：换行：」』】）也。

        空返回乃字符串列也。
        空返回者（默认返回于【『inttoptr i64 0 to i64*』】）也。

        创建数组乃化字符串而化字符串列而字符串列也。
        创建数组者会名而会参数而
            虑临时名者下个字符序数于名而
            虑长度字符者整数表示于（长度于参数）而
            『%"』衔名衔『" = call i64* @yy_gcAllocateArray(i64 』衔（长度字符）衔『)「：换行：」』衔
            『%"』衔临时名衔保留字段衔『" = bitcast i64* %"』衔名衔『" to i64**「：换行：」』衔
            （「串联」于（
                                            态射序号于（会参数序数而会参数名而
                『%"』衔临时名衔保留字段衔『_』衔（整数表示于参数序数）衔『" =getelementptr i64*, i64** %"』衔临时名衔保留字段衔『" , i64 』衔（整数表示于参数序数）衔『「：换行：」』衔
                『store i64* %"』衔（参数名）衔『", i64** %"』衔临时名衔保留字段衔『_』衔（整数表示于参数序数）衔『"「：换行：」』衔【】
                                            ）于参数
                ））
            也。

        「： 创建栈数组乃化字符串而化字符串列而字符串列也。
        创建栈数组者会名而会参数而
            虑临时名者下个字符序数于名而
            虑长度字符者整数表示于（长度于参数）而
            『%"』附名附『" = alloca i64, i64 』附（长度字符）附『「：换行：」』附
            『%"』附临时名附保留字段附『" = bitcast i64* %"』附名附『" to i64**「：换行：」』附
            （以『』合并（
                                            态射序号于（会参数序数而会参数名而
                『%"』附临时名附保留字段附『_』附（整数表示于参数序数）附『" =getelementptr i64*, i64** %"』附临时名附保留字段附『" , i64 』附（整数表示于参数序数）附『「：换行：」』附
                『store i64* %"』附（参数名）附『", i64** %"』附临时名附保留字段附『_』附（整数表示于参数序数）附『"「：换行：」』
                                            ）于参数
                ））
            也。 ：」

        转换结果乃字符串列也。
        转换结果者
        尝试运行于 （
         会无而
            鉴式而
                有（自由变量于名）则
                        默认返回于（『bitcast i64* %"』衔名衔『" to i64*』衔【】）
                或有（式节点于（非自由变量节点于「变量名」）于【】）则
                    （
                        默认返回于（『bitcast i64* %"』衔「变量名」衔『" to i64*』衔【】）
                    ）
                或有（式节点于（自由变量标注节点于「标注名」）于（自由变量于「新名」）衔【】）则
                        默认返回于（『bitcast i64* %"』衔新名衔『" to i64*』衔【】）
                「： 或有（式节点于（正则结构结尾节点无类型）于「参数」）则
                    （虑参数们者态射于（会参而
                        鉴参而有（式节点于（自由变量标注节点于「投名」）于（自由变量于「参名」）衔【】）
                            则参名
                            或有「其他」则发生事故于『113，不正确，闭包转换不应该改变正则结构结尾参数节点』
                            ）于参数而
                        创建数组于返回名于参数们
                      「： 虑数组创建函数者创建固定长度的数组于写出于（长度于参数们）而
                        默认返回于（『call 』衔默认调用规范衔『 i64* @"』衔（数组创建函数）衔『"』衔（获取普通参数签名于（参数们））） ：」
                    ） ：」
                或有（式节点于（文件引用节点于「文件名」）于【】）则
                    （
                        若（读引用于「使用全局编译」）
                        则（
                            发生事故于『全局编译：不应当出现文件引用』
                        ）否则（
                            虑无者「添加文件引用」于文件名而
                            「： 虑文件全局名者（读取代码生成信息于文件名）中零而 ：」
                                默认返回于（『load i64*, i64** @"』衔（当前路径转模块全局名于文件名）衔『"』衔【】）
                        ）
                    ）
                「： 或有（式节点于（结构投影带序数于「投影名」于「序数」）于「结构」衔【】）则
                    （
                        虑结构名者读名于结构而
                        （
                            『%"结构体指针』附保留字段附『" = bitcast i64* %"』附结构名附『" to i64**「：换行：」』附
                            『%"返回值指针』附保留字段附『" = getelementptr i64*, i64** %"结构体指针』附保留字段附『", i64 』附（整数表示于序数）附『「：换行：」』附
                            （默认返回于『load i64*, i64** %"返回值指针』附保留字段附『"』）
                        ）
                    ） ：」
                或有（式节点于（展开后内建节点于量）于「参数们」）则
                    （
                        虑参数名们者态射于「读名」于参数们而
                        鉴量而
                        「： 有「内建类型字符串」      则（空返回）
                        或有「内建类型整数」        则（空返回）
                        或有「内建类型小数」       则（空返回）
                        或有「内建类型动态分类值」     则（空返回）
                        或有「内建类型有」         则（空返回）
                        或有「内建类型爻」         则（空返回）
                        或有「内建类型元类型」       则（空返回） ：」
                        有「内建爻阳」          则（
                                                        （默认返回于『inttoptr i1 1 to i64*』衔【】）
                                                ）
                        或有「内建爻阴」          则（
                                                        （默认返回于『inttoptr i1 0 to i64*』衔【】）
                                                ）
                        或有「内建有元」          则（空返回）
                        或有「内建函数整数相等」    则
                                （鉴参数名们而
                                有「左名」衔「右名」衔【】则（
                                        『%"左值』衔保留字段衔『" = ptrtoint i64* %"』衔（左名）衔『" to i64「：换行：」』衔
                                        『%"右值』衔保留字段衔『" = ptrtoint i64* %"』衔（右名）衔『" to i64「：换行：」』衔
                                        『%"结果值』衔保留字段衔『" = icmp eq i64 %"左值』衔保留字段衔『", %"右值』衔保留字段衔『"「：换行：」』衔
                                        （默认返回于『inttoptr i1 %"结果值』衔保留字段衔『" to i64*』衔【】）
                                ）
                                或有「其他」则（发生事故于『期待两个参数』）
                                ）
                        或有「内建函数整数大于」    则
                                （鉴参数名们而
                                有「左名」衔「右名」衔【】则（
                                    『%"左值』衔保留字段衔『" = ptrtoint i64* %"』衔（左名）衔『" to i64「：换行：」』衔
                                    『%"右值』衔保留字段衔『" = ptrtoint i64* %"』衔（右名）衔『" to i64「：换行：」』衔
                                    『%"结果值』衔保留字段衔『" = icmp sgt i64 %"左值』衔保留字段衔『", %"右值』衔保留字段衔『"「：换行：」』衔
                                    （默认返回于『inttoptr i1 %"结果值』衔保留字段衔『" to i64*』衔【】）
                                ）
                                或有「其他」则（发生事故于『期待两个参数』）
                                ）
                        或有「内建函数整数减」    则
                                （鉴参数名们而
                                有「左名」衔「右名」衔【】则（
                                        『%"左值』衔保留字段衔『" = ptrtoint i64* %"』衔（左名）衔『" to i64「：换行：」』衔
                                        『%"右值』衔保留字段衔『" = ptrtoint i64* %"』衔（右名）衔『" to i64「：换行：」』衔
                                        『%"结果值』衔保留字段衔『" = sub i64 %"左值』衔保留字段衔『", %"右值』衔保留字段衔『"「：换行：」』衔
                                        （默认返回于『inttoptr i64 %"结果值』衔保留字段衔『" to i64*』衔【】）
                                ）
                                或有「其他」则（发生事故于『期待两个参数』）
                                ）
                        或有 「其他」            则（发生事故于『未有213：』附（表达式表示于（式）））
                    ）
                「： 或有（式节点于（空结构节点）于【】）则
                    （空返回）
                或有（式节点于（签名节点于「可能标签名」）于参）则
                    （空返回）
                或有（式节点于（空签名节点）于【】）则
                    （空返回）
                或有（式节点于（签名对象节点）于「签名」衔【】）则
                    （空返回） ：」
                或有（式节点于（整数节点于数）于【】）则
                    （
                        （默认返回于『inttoptr i64 』衔（整数表示于数）衔『 to i64*』衔【】）
                    ）
                或有（式节点于（「小数节点」于数于「整小部」）于【】）则
                    （ 虑小名者下个字符序数于『小数值』而
                        『%"』衔（小名）衔『" = bitcast double 』衔（（「整小部」中零））衔（『.』）衔（（「整小部」中一））衔『 to i64「：换行：」』衔
                        （默认返回于『inttoptr i64 %"』衔「小名」衔『" to i64*』衔【】）
                    ）
                或有（式节点于（字符串节点于串）于【】）则
                    （
                        虑串名者下个字符序数于『字符串』附保留字段而
                        （写出于（
                            【字符串声明于串名于串】
                        ）
                        ；
                        （默认返回于『bitcast 』衔（字符串声明类型于串）衔（『* @"』）衔「串名」衔（『" to i64*』）衔【】））
                    ）
                或有（式节点于（元组构造节点）于「参数」）则
                    （
                        虑元素名们者态射于读名于参数而
                        创建数组于返回名于元素名们
                    ）
                「： 或有（式节点于（栈元组构造节点）于「参数」）则
                    （
                        虑元素名们者态射于读名于参数而
                        创建栈数组于返回名于元素名们
                    ） ：」
                「： 或有（式节点于（擦除后类型节点）于【】）则
                    （空返回） ：」
                或有（式节点于（元组解构节点于「序数」）于「元组」衔【】）则
                    （
                        虑元组名者读名于元组而
                            『%"元组数组』衔保留字段衔『" = bitcast i64* %"』衔元组名衔『" to i64**「：换行：」』衔
                            『%"目标指针』衔保留字段衔『" = getelementptr i64*, i64** %"元组数组』衔保留字段衔『", i64 』衔（整数表示于序数）衔『「：换行：」』衔
                            （默认返回于（『load i64*, i64** %"目标指针』衔保留字段衔『"』衔【】））
                    ）
                或有（式节点于（元组更新节点于「序数」）于「元组」衔「新值」衔【】）则
                    （
                        虑元组名者读名于元组而
                        虑新值名者读名于新值而
                            『%"元组数组』衔保留字段衔『" = bitcast i64* %"』衔元组名衔『" to i64**「：换行：」』衔
                            『%"目标指针』衔保留字段衔『" = getelementptr i64*, i64** %"元组数组』衔保留字段衔『", i64 』衔（整数表示于序数）衔『「：换行：」』衔
                            『store i64* %"』衔（新值名）衔『", i64** %"目标指针』衔保留字段衔『"「：换行：」』衔
                            （空返回）
                    ）
                「： 或有（式节点于（展开后唯一构造器节点于「序数」）于「参数」）则
                    （
                        虑参数名们者态射于读名于参数而
                        『%"构造器序数』衔保留字段衔『" = inttoptr i64 』衔（整数表示于序数）衔『 to i64*「：换行：」』衔
                        （创建数组于返回名于（（『构造器序数』附保留字段）衔参数名们））
                    ） ：」
                「： 或有（式节点于（栈展开后唯一构造器节点于「序数」）于「参数」）则
                    （
                        虑参数名们者态射于读名于参数而
                        『%"构造器序数』衔保留字段衔『" = inttoptr i64 』衔（整数表示于序数）衔『 to i64*「：换行：」』衔
                        （创建栈数组于返回名于（（『构造器序数』附保留字段）衔参数名们））
                    ） ：」
                或有（式节点于（「解析后外部调用节点无类型」于串）于「参数」）则
                （
                    虑参数名们者态射于读名于参数而
                    虑无者「添加外部调用」于串于（长度于参数）而
                            （默认返回于（『call i64* @"』衔（串）衔『"(』衔（
                                        （「串联」于（以（【『,』】）合并列（
                                            （态射于（会参数名而（ 『i64* %"』衔（参数名）衔『"』衔【】 ））于参数名们）
                                        ）））
                                    ）接（『)』衔【】）））
                ）
                或有（式节点于空值节点于【】）则
                    （默认返回于（『bitcast i64* null to i64*』衔【】））
                或有（式节点于（全局路径变量存储节点于「全局路径名」）于参衔【】）则
                    （
                        「： 虑无者「添加文件引用」于全局路径名而 ：」
                        虑参名者读名于参而
                            『store i64* %"』衔参名衔『", i64** @"』衔（当前路径转模块全局名于「全局路径名」）衔『"「：换行：」』衔
                            （空返回）
                    ）
                或有（式节点于（函数引用节点于「函数名」）于【】）则
                （
                    默认返回于『bitcast i64* (i64*, i64*, i64*, i64*)* @"』衔（函数名）衔『" to i64*「：换行：」』衔【】
                ）
                或有「其他」则发生事故于『未有直接代码生成497：』附（表达式表示于式）
        ） 
            于（会异常而
                发生事故于异常附『「：换行：」在直接代码生成』附（表达式表示于式）附『时』
        ） 
        也。

        非常详细调试打印行于（会无而『直接代码生成完成：』附（表达式表示于式）附『。结果是：』 ）。
        转换结果
」
也。